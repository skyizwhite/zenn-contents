---
title: "【Common Lisp】defclassのスロットを型安全にする"
emoji: "☯️"
type: "tech"
topics:
  - "commonlisp"
published: true
published_at: "2023-07-27 11:25"
---

注 1: 本記事は 2023 年 3 月に筆者が個人サイト([skyizwhite.dev](https://skyizwhite.dev) 現在は閉鎖)で公開した記事と同一の内容です
注 2: Common Lisp 処理系は SBCL 2.3.1 を使用しています

## defclass の型注釈は型安全を保証しない

`defclass` マクロでクラスを定義する際、スロットに対して下記のように型注釈を付けることができます。

```lisp
(defclass person ()
  ((name :type     string
         :accessor person-name
         :initarg  :name)
   (age  :type     integer
         :accessor person-age
         :initarg  :age)))

(defun make-person (&key name age)
  (make-instance 'person
                 :name name
                 :age  age))
```

それでは person クラスのインスタンスを作ってみましょう... おっと手が滑った

```lisp
CL-USER> (make-person :name 20 :age "taro")
#<PERSON {701189FBE3}>
CL-USER> (person-name *)
20
```

型注釈と異なる型の値が格納されましたが、エラーは発生していません。何故こうなったかを調べてみたところ、

> The :type slot option specifies that the contents of the slot will always be of the specified data type. It effectively declares the result type of the reader generic function when applied to an object of this class. The consequences of attempting to store in a slot a value that does not satisfy the type of the slot are undefined.

(出典: [CLHS: Macro DEFCLASS - LispWorks](http://www.lispworks.com/documentation/HyperSpec/Body/m_defcla.htm))

との記述があり、異なる型を持つ値がスロットに格納された時の結果は未定義とのこと。そこで、この操作の結果としてエラーを発火させて型安全にする方法について調べてみました。

## 解決策その 1：最適化オプション safety を利用する

SBCL の処理系依存の機能ですが、クラス定義やスロットアクセスを行う箇所で`(declaim (optimize safety))`という最適化オプションを適用することで、値の格納時に型判定が走るようになります。

## 解決策その 2：スロット格納時に型判定を行うメタクラスを導入する

次に示す方法は[MOP(MetaObject Protocol)](https://franz.com/support/documentation/10.1/doc/mop/index.html)を利用する方法です。MOP は ANSI Common Lisp の仕様には含まれませんが、様々な処理系に実装されているデファクトスタンダードの機能です。処理系依存の機能なので、実装ではそれらの差異を吸収した[Closer to MOP](https://github.com/pcostanza/closer-mop)というライブラリを利用します。

下記のように、valid-class というメタクラスと、スロット格納時に型判定を行うメソッドを実装します。

```lisp
(ql:quickload :closer-mop)

(defclass valid-class (c2mop:standard-class)
  ())

(defmethod c2mop:validate-superclass ((class valid-class)
                                      (superclass c2mop:standard-class))
  t)

(defmethod (setf c2mop:slot-value-using-class) :before (new-value
                                                        (class valid-class)
                                                        object
                                                        slot)
  (let ((slot-type (c2mop:slot-definition-type slot)))
    (unless (typep new-value slot-type)
      (error 'type-error :datum new-value :expected-type slot-type))))
```

`defclass`マクロ呼び出しで先ほど作成した`valid-class`をメタクラスとして指定することで、スロット格納時に型判定が走るクラスを定義することができます。

```lisp
(defclass person ()
  ((name :type     string
         :accessor person-name
         :initarg  :name)
   (age  :type     integer
         :accessor person-age
         :initarg  :age))
  (:metaclass valid-class))
```

それでは実際に valid-class をメタクラスに持つ person クラスのインスタンスを作ってみましょう。

```lisp
CL-USER> (make-person :name 20 :age "taro")
The value
  20
is not of type
  STRING
   [Condition of type TYPE-ERROR]

CL-USER> (make-person :name "taro" :age "20")
The value
  "20"
is not of type
  INTEGER
   [Condition of type TYPE-ERROR]

CL-USER> (make-person :name "taro" :age 20)
#<PERSON {7011D717F3}>

CL-USER> (setf (person-age *) "20")
The value
   "20"
is not of type
   INTEGER
    [Condition of type TYPE-ERROR]
```

スロットの型判定が行われていますね 👌

## 参考文献

- [CLHS: Macro DEFCLASS - LispWorks](http://www.lispworks.com/documentation/lw71/CLHS/Body/m_defcla.htm)
- [SBCL 2.3.2 User Manual](https://www.sbcl.org/manual/)
- [The Common Lisp Object System MetaObject Protocol](https://franz.com/support/documentation/10.1/doc/mop/index.html)
